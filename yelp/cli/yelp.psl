//SIMILARITIES
//ITEMS
//pearson items
1.0 :  rated(U,I1) & rated(U,I2) & rating(U,I1) & sim_pearson_items(I1,I2) >> rating(U,I2)
//cosine items
1.0 :  rated(U,I1) & rated(U,I2) & rating(U,I1) & sim_cosine_items(I1,I2) >> rating(U,I2)
//adj cosine items
1.0 :  rated(U,I1) & rated(U,I2) & rating(U,I1) & sim_adjcos_items(I1,I2) >> rating(U,I2)

//USERS
//pearson users
1.0 :  rated(U1,I) & rated(U2,I) & rating(U1,I) & sim_pearson_users(U1,U2) >> rating(U2,I)
//cosine users
1.0 :  rated(U1,I) & rated(U2,I) & rating(U1,I) & sim_cosine_users(U1,U2) >> rating(U2,I)

//low dimension space similarities
//USERS
1.0 :  user(U1) & user(U2) & item(I) & rating(U1,I) & rated(U1,I) &rated(U2,I) & sim_mf_cosine_users(U1,U2) >> rating(U2,I)
1.0 :  user(U1) & user(U2) & item(I) & rating(U1,I) & rated(U1,I) &rated(U2,I) & sim_mf_euclidean_users(U1,U2) >> rating(U2,I)

//ITEMS		
1.0 :  user(U) & item(I1) & item(I2) & rating(U,I1) & rated(U,I1) & rated(U,I2) & sim_mf_cosine_items(I1,I2) >> rating(U,I2)
1.0 :  user(U) & item(I1) & item(I2) & rating(U,I1) & rated(U,I1) & rated(U,I2) & sim_mf_euclidean_items(I1,I2) >> rating(U,I2)

//PREDICTIONS
//SGD Predictions
1.0 : sgd_rating(U,I) >> rating(U,I)
1.0 : rating(U,I) >> sgd_rating(U,I)

//item based pearson Predictions
1.0 : item_pearson_rating(U,I) >> rating(U,I)
1.0 : rating(U,I) >> item_pearson_rating(U,I)

//BPMF predictions
1.0 : bpmf_rating(U,I) >> rating(U,I)
1.0 : rating(U,I) >> bpmf_rating(U,I)

//AVERAGE PRIORS
1.0  : user(U) & item(I) & rated(U,I) & avg_user_rating(U) >> rating(U,I)
1.0  : user(U) & item(I) & rated(U,I) & rating(U,I) >> avg_user_rating(U)

//items tend to get ratings towards their average
1.0  : user(U) & item(I) & rated(U,I) & avg_item_rating(I) >> rating(U,I)
1.0  : user(U) & item(I) & rated(U,I) & rating(U,I) >> avg_item_rating(I)

//SOCIAL RULES
//friendships. This rule is given weight of 4 by the original author
1.0 : rated(U1,I) & rated(U2,I) & users_are_friends(U1,U2) & rating(U1,I) >> rating(U2,I)

//CONTENT RULES
1.0  :  rated(U,I1) & rated(U,I2) & rating(U,I1) & sim_content_items_jaccard(I1,I2) >> rating(U,I2)
